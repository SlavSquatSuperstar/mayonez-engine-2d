plugins {
    id("application") // For creating runnable programs

    id("com.github.johnrengelman.shadow") version "8.1.1" // For making fat jars
    id("org.jetbrains.dokka") version "1.8.20" // For Kotlin documentation, must apply individually
    id("org.jetbrains.kotlin.jvm") version "1.9.0" apply false // For compiling Kotlin files
}

// Project Info

description = "The root project for Mayonez Engine that contains all modules."

apply(from: "gradle/get_os.gradle")

ext {
    javaVersion = "17"
    mainClass = "slavsquatsuperstar.demos.DemosLauncher"
    mainModule = "mayonez.demos"
}

// Project Settings
allprojects {
    // Name
    group = "slavsquatsuperstar"
    version = "0.7.9-release"

    // Java Version, set for all tasks
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(project.javaVersion)
        }
    }

    // Dependencies
    repositories { mavenCentral() }
}

dependencies {
    // Subprojects
    implementation(project(":mayonez-base"))
    implementation(project(":mayonez-demos"))
}

// Tasks

application { // For running project
    mainClass = project.mainClass
    mainModule = project.mainModule
    if (macOS) applicationDefaultJvmArgs = ["-XstartOnFirstThread"] // For LWJGL on macOS
}

shadowJar { // For building jar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier = ""
    mainClass = project.mainClass
}