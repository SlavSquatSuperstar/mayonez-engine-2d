import org.gradle.internal.os.OperatingSystem

plugins {
    id 'idea'
    id 'java'
    id 'application'

    id 'org.jetbrains.kotlin.jvm' version '1.8.0'
    id 'org.jetbrains.dokka' version '1.7.20' // For Kotlin documentation
    id 'com.github.johnrengelman.shadow' version '7.1.2' // For making fat jars
}

// Project Info

description = 'The root project for Mayonez Engine that contains all modules.'

project.ext {
    // Custom Settings
    javaVersion = '17'
    mainClass = 'slavsquatsuperstar.demos.DemosLauncher'
    mainModule = 'mayonez.demos'
}

allprojects {
    // Name
    group 'slavsquatsuperstar'
    version '0.7.5'

    // Java Version
    sourceCompatibility = targetCompatibility = javaVersion

    // Dependencies
    repositories {
        mavenCentral()
    }
}

dependencies {
    // Subprojects
    implementation project(':mayonez-base')
    implementation project(':mayonez-demos')
}

// Tasks and Plugins

application { // For 'run' task
    mainClass = project.ext.mainClass
    mainModule = project.ext.mainModule
}

run {
    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        jvmArgs = ["-XstartOnFirstThread"] // For LWJGL
        args = ["true"] // Set CLI args (Use GL true/false)
    } else {
        args = ["false"]
    }
}

jar {
    manifest.attributes('Main-Class': project.ext.mainClass)
    // 'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')
}

shadowJar {
    dependsOn jar
    archiveClassifier.set('')
    manifest.from jar.manifest
}