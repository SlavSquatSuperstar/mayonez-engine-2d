plugins {
    id 'application'

    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
    id 'org.jetbrains.dokka' version '1.7.20' // For Kotlin documentation
    id 'com.github.johnrengelman.shadow' version '7.1.2' // For making fat jars
}

// Project Info

description = 'The root project for Mayonez Engine that contains all modules.'

apply from: 'gradle/dependencies.gradle'

// Custom Settings
allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    // Name
    group 'slavsquatsuperstar'
    version '0.7.6-pre2-snapshot'

    // Java Version
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    // Dependencies
    repositories { mavenCentral() }
}

dependencies {
    // Subprojects
    implementation project(':mayonez-base')
    implementation project(':mayonez-demos')
}

// Tasks and Plugins

ext {
    mainClass = 'slavsquatsuperstar.demos.DemosLauncher'
    mainModule = 'mayonez.demos'
}

application { // For 'run' task
    mainClass = project.ext.mainClass
    mainModule = project.ext.mainModule
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest { attributes 'Main-Class': project.ext.mainClass }
    // 'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')
}

shadowJar {
    dependsOn jar
    archiveClassifier.set('')
    manifest.from jar.manifest
}

run {
    dependsOn shadowJar
    if (macOS) jvmArgs = ["-XstartOnFirstThread"] // For LWJGL on macOS
    args = ["--engine gl"] // Set CL args (use GL true/false)
}

// Shadow Jar Dependencies

distTar { dependsOn shadowJar }
distZip { dependsOn shadowJar }
startScripts { dependsOn shadowJar }