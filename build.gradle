plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'org.jetbrains.dokka' version '1.6.0' // For Kotlin Documentation
    id 'com.github.johnrengelman.shadow' version '7.1.1' // For making fat jars
}

group 'slavsquatsuperstar'
version '0.7.2-SNAPSHOT-4'

repositories.mavenCentral()

// LWJGL Settings
project.ext.lwjglVersion = '3.3.0'
project.ext.lwjglNatives = 'natives-macos' // Configure LWJGL OS here

dependencies {
    // Code Dependencies
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.6.10'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.json:json:20211205'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'org.slf4j:slf4j-simple:1.7.32'

    // LWJGL Libraries
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-nfd"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nfd::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    implementation "org.joml:joml:1.10.2"

    // Test Libraries
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
}

String javaVersion = '17'
sourceCompatibility = targetCompatibility = javaVersion
mainClassName = 'slavsquatsuperstar.Main'

compileKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
}

run {
    jvmArgs = ["-XstartOnFirstThread"] // For LWJGL on macOS
}

shadowJar {
    archiveClassifier.set('')
    manifest.attributes 'Main-Class': mainClassName
    exclude '**/LICENSE.txt', '**/NOTICE.txt' // avoid duplicate APACHE .txt files
}

test.useJUnitPlatform()